// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext {
        buildToolsVersion = "34.0.0"
        minSdkVersion = 23
        compileSdkVersion = 35
        targetSdkVersion = 35
        ndkVersion = "25.1.8937393"
        kotlinVersion = "1.9.22"
    }
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath('com.android.tools.build:gradle:8.13.2')
        classpath("com.facebook.react:react-native-gradle-plugin")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion")
    }
}

allprojects {
    repositories {
        maven {
            // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
            url("$rootDir/../node_modules/react-native/android")
        }
        maven {
            // Android JSC is installed from npm
            url("$rootDir/../node_modules/jsc-android/dist")
        }
        mavenCentral {
            // We don't want to fetch react-native from Maven Central as there are
            // older versions over there.
            content {
                excludeGroup "com.facebook.react"
            }
        }
        google()
        maven { url 'https://www.jitpack.io' }
    }
}

subprojects {
    afterEvaluate { project ->
        if (project.hasProperty("android")) {
            android {
                compileSdkVersion rootProject.ext.compileSdkVersion
            }
        }
        
        // Fix for React Native modules that can't find BaseReactPackage and codegen interfaces
        // Add compatibility module to React Native library modules
        if (project.plugins.hasPlugin("com.android.library") && project.path != ":react-native-compat") {
            def isReactNativeModule = project.path.contains("react-native") || 
                                     project.name.contains("react-native") ||
                                     project.projectDir.path.contains("react-native")
            
            if (isReactNativeModule) {
                def compatProject = rootProject.findProject(":react-native-compat")
                // Add compatibility module for BaseReactPackage and ViewManagerWithGeneratedInterface
                if (compatProject != null) {
                    project.dependencies.add("implementation", compatProject)
                }
            }
        }
    }
}
